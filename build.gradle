/**
Template file for classes using GradleRIO
@author Jaci
*/

buildscript {
  repositories {
	mavenCentral()
	maven {
		name = "GradleRIO"
		url = "http://dev.imjac.in/maven"
	}
  }
  dependencies {
    classpath group: 'jaci.openrio.gradle', name: 'GradleRIO', version: '+'			//Change this line if you wish to Update GradleRIO
    classpath 'org.ajoberstar:grgit:1.1.0'
  }
}

apply plugin: 'java'
dependencies {
  compile files('lib/navx_frc.jar')
}

apply plugin: 'idea'

apply plugin: 'application'
ext {
  git = org.ajoberstar.grgit.Grgit.open(file('.'))        //Open git repository
  revision = git.head().id                                //Get commit id for revision
}

version = "2.0.1.${git.head().abbreviatedId}"             //Get commit abbreviatedId for verision
mainClassName = 'src.main.java.org.usfirst.frc.team5687.robot.Robot'    //Application plugin extension properties
applicationName = 'Team 5687'

applicationDistribution.with {
  from('src/dist') {
    include 'VERSION'
    expand(
      buildDate: new Date(),
      revision: revision,
      version: project.version,
      appName: applicationName)
  }
}

assemble.dependsOn installDist

//apply plugin: 'eclipse'
apply plugin: 'GradleRIO'                                 //Apply the GradleRIO plugin

gradlerio.robotClass = "org.usfirst.frc.team5687.robot.Robot"   //The class for the main Robot Class. Used in manifest
gradlerio.team = "5687"                                   //The FRC team number
gradlerio.rioIP = "10.56.87.2"                            //The IP address of the RIO

def robotManifest = {
  attributes 'Main-Class': 'edu.wpi.first.wpilibj.RobotBase'
  attributes 'Robot-Class': gradlerio.robotClass
}

jar {
  from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  manifest robotManifest
}

task genJavadoc(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives genJavadoc
}
